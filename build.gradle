import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
    repositories {
        mavenCentral()
    }
}
plugins {
    id 'pl.allegro.tech.build.axion-release' version '1.18.18'
    id 'java'
}

ext.pluginName = 'Openssh node execution services'
ext.publishName = "Openssh node execution services ${project.version}"
ext.publishDescription = project.description ?: 'Openssh node execution services'
ext.githubSlug = 'rundeck-plugins/openssh-node-execution'
ext.pluginDescription = "Executes a openssh commands on the node. Password Authentication needs sshpass installed on the rundeck server"
ext.sopsCopyright = "Â© 2017, Rundeck, Inc."
ext.sopsUrl = "http://rundeck.com"
ext.buildDateString = new Date().format("yyyy-MM-dd'T'HH:mm:ssX")
ext.archivesBaseName = "openssh-node-execution"
ext.pluginBaseFolder = "."
ext.developers = [
        [id: 'gschueler', name: 'Greg Schueler', email: 'greg@rundeck.com']
]

scmVersion {
    ignoreUncommittedChanges = true
    tag {
        prefix = ''
        versionSeparator = ''
    }
    versionCreator("simple")
}

project.version = scmVersion.version
ext.archiveFilename = ext.archivesBaseName + '-' + version

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

defaultTasks 'build'

tasks.register('pluginZip', Jar) {
    archiveBaseName.set(project.ext.archivesBaseName)
    archiveVersion.set(project.version)
    archiveClassifier.set('')
    destinationDirectory.set(file("build/libs"))
    extension = 'zip'

    from("${project.buildDir}/zip-contents") {
        include("*.yaml")
        include("resources/**")
        include("contents/*")
        into("${archiveBaseName.get()}-${archiveVersion.get()}")
    }

    manifest {
        attributes(
                'Rundeck-Plugin-Name': pluginName.toString(),
                'Rundeck-Plugin-Description': pluginDescription.toString(),
                'Rundeck-Plugin-Archive': 'true',
                'Rundeck-Plugin-File-Version': version,
                'Rundeck-Plugin-Author': sopsCopyright,
                'Rundeck-Plugin-URL': sopsUrl,
                'Rundeck-Plugin-Date': buildDateString
        )
    }
}

tasks.named('pluginZip').configure {
    doFirst {
        def assetsMap = new Properties()
        def tokens = assetsMap + [
                version    : version,
                date       : new Date().format("yyyy-MM-dd'T'HH:mm:ssX").toString(),
                author     : sopsCopyright,
                url        : sopsUrl,
                title      : pluginName,
                description: pluginDescription,
                name       : archivesBaseName.toString(),
        ]

        copy {
            from("${project.projectDir}/resources") {
                include '**/*'
                into "resources"
            }
            from("${project.projectDir}/contents") {
                into "contents"
            }
            from("${project.projectDir}/plugin.yaml") {
                filter(ReplaceTokens, tokens: tokens)
                exclude '**/*.png'
            }
            into "${project.buildDir}/zip-contents"
        }
    }
}

apply plugin: 'maven-publish'

publishing {
    publications {
        mavenZip(MavenPublication) {
            artifact tasks.named('pluginZip').get()
        }
    }
}

defaultTasks 'clean', 'build', 'pluginZip'

tasks.named('build').configure {
    dependsOn tasks.named('pluginZip')
}